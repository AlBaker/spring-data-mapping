

subprojects { project ->

    version = '0.1'
  
	def projectName = project.toString()
    def isGrailsPlugin = projectName.contains(":grails-plugins")
	if(!isGrailsPlugin) {
	    apply plugin: 'groovy'
	    apply plugin: 'eclipse'
        apply plugin: 'maven'
	}

    repositories {
       mavenRepo name:'mavenLocal',
                 urls:new File( System.getProperty("user.home" ), ".m2/repository" ).toURL().toString()

       mavenCentral()
    }

    dependencies {
		if(!isGrailsPlugin) {
	       	groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.7.4'
	        compile "org.springframework:spring-core:3.0.3.RELEASE"
	        compile "org.springframework:spring-beans:3.0.3.RELEASE"
	        compile "org.springframework:spring-tx:3.0.3.RELEASE"
            compile "org.slf4j:jcl-over-slf4j:1.5.8"
            compile "org.slf4j:slf4j-api:1.5.8"
            compile "org.slf4j:slf4j-simple:1.5.8"

            testCompile 'junit:junit:4.8.1'
        }
    }


    if(projectName.contains(":grails-")) {
      group = "org.grails"
      archivesBaseName = "grails-datastore"
    }
    else {
      group = "org.springframework"
      archivesBaseName = 'spring-datastore'
    }

    if(!isGrailsPlugin) {
      configure(install.repositories.mavenInstaller) {
          pom.whenConfigured { pom ->
            pom.dependencies.find { dep -> dep.artifactId == 'slf4j-simple' }.optional = true
          }
          pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'http://github.com/grails/inconsequential'
                }
            }
          }
      }
    }
}