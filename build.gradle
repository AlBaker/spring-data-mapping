
def spockDependency = "org.spockframework:spock-core:0.5-groovy-1.7-SNAPSHOT"

subprojects {

    version = '0.1'
  
    def isGrailsPlugin = project.name.startsWith("grails-plugins")
	if(!isGrailsPlugin) {
	    apply plugin: 'groovy'
	    apply plugin: 'eclipse'
        apply plugin: 'maven'
	}

    def isGormDatasource = project.name.startsWith("grails-datastore-gorm-") && !project.name.endsWith("tck")
    
    repositories {
       mavenRepo name:'mavenLocal',
                 urls:new File( System.getProperty("user.home" ), ".m2/repository" ).toURL().toString()
       mavenRepo name: "spock-snapshots", urls: ["http://m2repo.spockframework.org/snapshots"]
       mavenCentral()
    }

    dependencies {
		if(!isGrailsPlugin) {
	       	groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.7.4'
            testCompile 'junit:junit:4.8.1'
            testCompile spockDependency
        }
        
        if (project.name == "grails-datastore-gorm-tck") {
          compile spockDependency
        }
    }

    if (isGormDatasource) {
      dependencies {
        testCompile project(":grails-datastore-gorm-tck")
      }
      
      // We need to test against the TCK. Gradle cannot find/run tests from jars
      // without a lot of plumbing, so here we copy the class files from the TCK
      // project into this project's test classes dir so Gradle can find the test
      // classes and run them. See grails.gorm.tests.GormDatastoreSpec for on the TCK.
      test.doFirst {
        copy {
          from project(":grails-datastore-gorm-tck").sourceSets.main.classesDir
          into sourceSets.test.classesDir
          include "**/*Spec.class"
        }
      }
    }

    if(project.name.startsWith("grails-")) {
      group = "org.grails"
      archivesBaseName = "grails-datastore"
    }
    else {
      group = "org.springframework"
      archivesBaseName = 'spring-datastore'
    }

    if(!isGrailsPlugin) {
      configure(install.repositories.mavenInstaller) {
          pom.whenConfigured { pom ->
            def dependency = pom.dependencies.find { dep -> dep.artifactId == 'slf4j-simple' }
            dependency?.optional = true
          }
          pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'http://github.com/grails/inconsequential'
                }
            }
          }
      }
    }
}